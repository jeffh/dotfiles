"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
"                       General VIM settings                          "
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Use ! on function and command definitions to suppress warning
" on reload.
"
" This will automatically reload the vimfile on save.
" You can force it by doing :source % on the buffer that has this file
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

" Don't emulate vi bugs
set nocompatible
set shell=/bin/bash

"""""""""""""""""""""""""""""""""""""""""""""""
"                  VUNDLE                     "
"""""""""""""""""""""""""""""""""""""""""""""""
filetype off

call plug#begin('~/.vim/plugged')

" Theme
Plug 'w0ng/vim-hybrid'

"""""""""""
" Plugins "
"""""""""""

" Sensible
Plug 'tpope/vim-sensible'

" Autocomplete
Plug 'Shougo/neocomplete.vim'

" Ag / Silver Searcher
Plug 'rking/ag.vim'

" Repeat (Plug for plugins to use .)
Plug 'tpope/vim-repeat'

" gcc to (un)comment lines
Plug 'tpope/vim-commentary'

" Textmate-styled snippets
Plug 'Shougo/neosnippet'
Plug 'Shougo/neosnippet-snippets'

" File finding
Plug 'kien/ctrlp.vim'

" Paren, quotes, bracket editing
Plug 'tpope/vim-surround'

" AutoSave
Plug 'vim-auto-save'

" NERD tree
Plug 'scrooloose/nerdtree', { 'on': 'NERDTreeToggle' }

" Syntastic (syntax check)
Plug 'scrooloose/syntastic'

" Complementary pairing hotkeys & operations
" eg - ]q jumps to next in quicklist, [q for other way
"      ]space adds a new line below, [space adds new line above
Plug 'tpope/vim-unimpaired'

" Git
Plug 'tpope/vim-fugitive'

" Git-gutter
" Plug 'airblade/vim-gitgutter'

" status line
Plug 'bling/vim-airline'

" Tabular alignment
Plug 'vim-easy-align'

" Vipe
Plug 'luan/vipe'

"""""""""""""
" Languages "
"""""""""""""

" Ansible YAML
Plug 'chase/vim-ansible-yaml'

" Markdown
Plug 'godlygeek/tabular'
Plug 'plasticboy/vim-markdown'

" Terraform
Plug 'markcornick/vim-terraform'

" Mustache
Plug 'juvenn/mustache.vim'

" Google Go (golang)
Plug 'fatih/vim-go'

" Clojure
" Plug 'git://github.com/vim-scripts/paredit.vim.git'
Plug 'guns/vim-sexp', { 'for': 'clojure' } 
Plug 'guns/vim-clojure-static', { 'for': 'clojure' } 
Plug 'guns/vim-clojure-highlight', { 'for': 'clojure' }
Plug 'tpope/vim-fireplace', { 'for': 'clojure' }
Plug 'kien/rainbow_parentheses.vim'

" Python
Plug 'jmcantrell/vim-virtualenv'
Plug 'klen/python-mode'
Plug 'saltstack/salt-vim'

" Javascript
Plug 'pangloss/vim-javascript'

" Ruby
Plug 'tpope/vim-rvm'
Plug 'vim-ruby/vim-ruby'

call plug#end()

filetype plugin indent on
syntax enable

"""""""""""""""""""""""""""""""""""""""""""""""
"               KEY BINDINGS                  "
"""""""""""""""""""""""""""""""""""""""""""""""

" Special key for custom bindings
let mapleader = ","
let g:mapleader = ","

" fast editing of vimrc file
" ,v => edits current file
map <leader>v :e ~/.vimrc<CR>

" jump to previous buffer
noremap <leader><leader> <c-^>
map <c-c> <esc>

" Emacs keys for navigation
" Command Line
cnoremap <c-a> <home>
cnoremap <c-e> <end>
cnoremap <c-f> <right>
cnoremap <c-b> <left>
cnoremap <c-bs> <c-w>
" Normal, Visual modes
" noremap <c-f> l
" noremap <c-b> h
" noremap <c-p> k
" noremap <c-n> j
" noremap <c-k> D
" Insert mode
inoremap <c-f> <esc>la
inoremap <c-b> <esc>ha
inoremap <c-k> <esc>Da
"noremap <c-a> <esc>^i
inoremap <c-e> <esc>$a
inoremap <c-bs> <c-w>

" quickfix hotkeys
noremap <leader>j :cn<cr>
noremap <leader>k :cp<cr>

" Suppress all whitespace at end of lines when saving
" But preserve repeat macro & cursor position
function! <SID>StripTrailingWhitespaces()
    " Preparation: save last search, and cursor position.
    let _s=@/
    let l = line(".")
    let c = col(".")
    " Do the business:
    %s/\s\+$//e
    " Clean up: restore previous search history, and cursor position
    let @/=_s
    call cursor(l, c)
endfunction
noremap <leader>w :call <SID>StripTrailingWhitespaces()<cr>:w<cr>

" tab-navigation
noremap <c-h> gT
noremap <c-l> gt
noremap <c-t> :tabnew<cr>
noremap <c-c> :tabclose<cr>

" disable highlighting when hitting the return or esc key
nnoremap <CR> :nohlsearch<cr><cr>

" Type %/ or %? in command line to expand out to current buffer's file location
" (if it exists)
if has('unix')
    cnoremap %/ <C-R>=expand("%:h") . "/" <CR>
    cnoremap %? <C-R>=expand("%:h") . "/" <CR>
    cnoremap %% <C-R>=expand("%:h") . "/" <CR>
else
    cnoremap %/ <C-R>=expand("%:h") . "\\" <CR>
    cnoremap %? <C-R>=expand("%:h") . "\\" <CR>
    cnoremap %% <C-R>=expand("%:h") . "\\" <CR>
endif

" Smart tabbing behavior
function! CleverTab()
    if neosnippet#expandable_or_jumpable()
        return "\<Plug>(neosnippet_expand_or_jump)"
    endif
    if pumvisible()
        return "\<C-N>"
    endif
    let line_to_cursor = strpart( getline('.'), 0, col('.')-1 )
    if strlen( line_to_cursor ) == 0
        return "\<Tab>"
    elseif line_to_cursor =~ '\s\s*$'
        return "\<Tab>"
    elseif line_to_cursor =~ '/$'
        return "\<C-X>\<C-F>"
    elseif exists('&omnifunc') && &omnifunc != ''
        return "\<C-X>\<C-O>"
    else
        return "\<C-N>"
    endif
endfunction

imap <expr><tab> CleverTab()
inoremap <s-tab> <C-p>

" Duplicate current line
noremap Y <esc>yyp

" NERDTree toggle
noremap \ :NERDTreeToggle<CR>

" Compile ctags
nmap <leader>c :!ctags -R .<CR>

"""""""""""""""""""""""""""""""""""""""""""""""
"                CORE CONFIG                  "
"""""""""""""""""""""""""""""""""""""""""""""""

if $TMUX == ''
    set clipboard=unnamed  " System-like clipboard behavior if not in TMUX
endif

set tags=tags;$HOME " Search path for ctags

set notimeout          " Don't wait for timeout between command combinations

set nowrap             " no line wrapping
set textwidth=0        " width of document
set formatoptions-=t   " don't automatically wrap text when typing

set switchbuf="usetab" " buffer switching behavior - use tab or window if available

set showmatch          " show paren matching

set scrolloff=3        " Keep more context when scrolling off the end of a buffer
set so=7               " move 7 lines when moving vertical
set magic              " Set for regular expressions' backslashes

" visualize invisible characters. Show spaces at the end of lines and tabs.
set list
set listchars=tab:▸\ ,trail:·

set foldmethod=syntax  " code folding is determined by syntax
set foldlevel=99  " default fold level


""""""""""
" Search "
""""""""""

set hlsearch   " highlight search results
set incsearch  " incremental search
set ignorecase " that's case insensitive
set smartcase  " unless you type a capital letter
set wrapscan   " and the search wraps around the file

" make tab completion for files/buffers act like bash, but ignore sets of files
set wildignore=*.o,*.obj,*/.git/*,.svn,.cvs,*.rbc,*.pyc,*.class,*.jar,*.a
set wildignore+=*/build/*
set wildignore+=*/dist/*
set wildignore+=*/.ropeproject/*
set wildmode=longest:full:list
set wildmenu

" where <C-P> should get it's completion list
" . => current buffer
" w => buffers from other windows
" b => loaded buffers in buffer list
" u => unloaded buffers in buffer list (can be unreliable)
" U => buffers not in buffer list (can be unreliable)
" k => dictionary
" kspell => spell checking
" s => thesaurus
" i => scan current & included files
" d => scan current & included files for defined name or macro
" ] => tag completion
" t => alias for ]
set complete=.,w,b,t,d,i,t

" menu => Show menu when multiple autocomplete items are available
" preview => Show extra info about the currently selected completion in menu
" menuone => Shows menu when only one item is available for autocomplete
" longest => Only inserts longest common text for matches
set completeopt=menu,preview

set noinfercase  " autocomplete without being case sensitive

""""""""""""""""""""""""
" Files, Backups, Undo "
""""""""""""""""""""""""

set ffs=unix,dos,mac " default line endings

set history=600      " command line history
set undolevels=9000  " undo history

" auto reload vimrc when edited
augroup vim_autoreload
    autocmd!
    autocmd bufwritepost .vimrc source ~/.vimrc
augroup END

" vim's backup / swap system
set backupdir=~/.vim/tmp/,~/.tmp,/var/tmp,/tmp
set directory=~/.vim/tmp/,~/.tmp,/var/tmp,/tmp
" disable all swap, backup and writebackups
set nobackup     " no backups
set writebackup  " except when writing files
set noswapfile   " no swapfiles

" Persistent undo
try
    if has('win32')
        set undodir=C:\Windows\Temp
    else
        set undodir=/tmp/vim_undodir
    endif
    set undofile
catch
endtry

set autoread                   " auto refresh files that were changed
set virtualedit=all            " let cursor stray beyond textfile bounds
set backspace=eol,indent,start " backspace behaves like normal programs

" Auto save buffers when focus is lost
au FocusLost * silent! :wa
" Auto save when switching buffers
set autowrite
" remember marks & undos for background buffers
set hidden

set number " enable line numbers
"set numberwidth=5

" Set encoding and language
set encoding=utf8
try
    lang en_US
catch
endtry

autocmd BufReadPost *
    \ if line("'\"") > 0 && line("'\"") <= line("$") |
    \   exec "normal g`\"" |
    \ endif

"""""""
" GUI "
"""""""

" Customized titlebar, filename with 3 parent directories
auto BufEnter * let &titlestring=expand("%:p:h:h:h:t") . "/" . expand("%:p:h:h:t") . "/" . expand("%:p:h:t") . "/" . expand("%:p:t") . " - Vim"

" Always show status bar
set laststatus=2

" stylize the cursor in different modes
set guicursor=n-v-c:block-Cursor
set guicursor+=ve:ver35-Cursor
set guicursor+=i:blinkon0
set guicursor+=i:ver25-Cursor/lCursor

" Command bar height
set cmdheight=1

" No sound on errors or visual bells
set noerrorbells
set novisualbell
set t_vb=
set tm=500

set lazyredraw      " don't redraw when running macros
set shortmess=aOstT " shortens messages to avoid 'press a key' prompt

" show matching parens, brackets and braces
set showmatch
set matchtime=1

" Display cursor location info in the bottom right of status bar
set ruler

if has("gui_running")
    " disable toolbar & scrollbars
    set guioptions-=T
    set guioptions-=L
    set guioptions-=r

    set nospell

    if has('win32')
        " alt key jumps to menu
        set winaltkeys=menu
    end
    if has('macunix')
        set guifont=Monaco:h14
        set transparency=5
    end
endif

" Equalize window sizes when a window is removed
set equalalways

" highlight the line the cursor is on (can be slow for large files)
set nocursorline

" Experiemental performance improvements
set regexpengine=1
set scrolljump=3
syntax sync minlines=256
set norelativenumber

"""""""""
" Theme "
"""""""""
let &t_Co=256
colorscheme Hybrid

augroup color_customizations
    autocmd!
    if has('gui_running')
        autocmd ColorScheme * highlight CursorLine guibg=#292929
        autocmd ColorScheme * highlight LineNr guifg=Grey30 guibg=NONE
    else
        autocmd ColorScheme * highlight LineNr ctermfg=233 ctermbg=None
    endif
augroup END

"""""""""""""""""""""""""""""""""""""""""""""""
"        CUSTOM FUNCTIONS / COMMANDS          "
"""""""""""""""""""""""""""""""""""""""""""""""
command! ConvertToUTF8 :set encoding=utf-8 fileencodings=.

" Rename current file
function! RenameFile()
    let old_name = expand('%')
    let new_name = input('New file name: ', expand('%'))
    if new_name != '' && new_name != old_name
        exec ':saveas ' . new_name
        exec ':silent !rm ' . old_name
        exec '<c-^>:bw'
        redraw!
        exec ":echom \"Renamed " . old_name . " to " . new_name . "\""
    endif
endfunction
map <leader>r :call RenameFile()<cr>

function! DeleteFile()
    call inputsave()
    let file_name = input('Delete: ', expand('%'), "file")
    call inputrestore()
    exec ':silent !rm ' . file_name
    exec ':silent bw'
    redraw!
    exec ":echom \"Deleted " . file_name . "\""
endfunction
map <leader>d :call DeleteFile()<cr>

"""""""""""""""""
" Running Tests "
"""""""""""""""""
function! RunIfMatchesFile(command, file_ext_regex)
    let in_runnable_file = match(expand("%"), a:file_ext_regex) != -1
    if in_runnable_file
        let t:last_ran_file=@%
    elseif !exists("t:last_ran_file")
        return 0
    elseif match(expand("%"), a:file_ext_regex) == -1
        return 0
    end

    :silent w
    return RunCommand(a:command . t:last_ran_file)
endfunction

function! RunCommand(command)
    " :silent !echo;echo;echo;echo;echo;echo;echo;echo;echo
    " :silent !echo;echo;echo;echo;echo;echo;echo;echo;echo
    " :silent !echo;echo;echo;echo;echo;echo;echo;echo;echo
    " :silent !echo;echo;echo;echo;echo;echo;echo;echo;echo
    " :silent !echo;echo;echo;echo;echo;echo;echo;echo;echo
    " exec ":!" . a:command
    exec ":Vipe " . a:command
    return 1
endfunction

function! RunTestFileIfPossible()
    let t:skip_file_check=0
    let has_ran=0
    " Ruby
    if filereadable("Gemfile")
        let has_ran = RunIfMatchesFile("bundle exec rspec ", '\(.feature|_spec.rb\)$')
    elseif executable('rspec')
        let has_ran = RunIfMatchesFile("rspec ", '\(.feature|_spec.rb\)$')
    endif
    " Python
    if executable('py.test') == 1
        let has_ran = RunIfMatchesFile("py.test ", '.*test.*.py$')
    elseif executable('nosetests') == 1
        let has_ran = RunIfMatchesFile("nosetests ", '.*test.*.py$')
    endif

    if !has_ran
        echom "Failed to discover test runner. Please bind manually."
    endif
endfunction

function! RunTestsIfPossible()
    let t:skip_file_check=0
    let has_ran=0
    " Ruby
    if filereadable("Gemfile")
        let has_ran = RunCommand("bundle exec rspec")
    elseif executable('rspec')
        let has_ran = RunCommand("rspec")
    endif
    " Python
    if executable('py.test') == 1
        let has_ran = RunCommand("py.test")
    elseif executable('nosetests') == 1
        let has_ran = RunCommand("nosetests")
    endif

    if !has_ran
        echom "Failed to discover test runner. Please bind manually."
    endif
endfunction

nnoremap <leader>t :call RunTestFileIfPossible()<cr>
nnoremap <leader>T :call RunTestsIfPossible()<cr>


"""""""""""""""""""""""""""""""""""""""""""""""
"                LANG CONFIG                  "
"""""""""""""""""""""""""""""""""""""""""""""""

""""""""""""
" Defaults "
""""""""""""
set expandtab          " use spaces for tab key
set autoindent         " automatically detect how to indent
set smarttab           " be smart to how much to indent
set cindent            " be smart for indenting a new line
set shiftround         " round indent to multiple of shiftwidth

set shiftwidth=4       " indent amounts
set tabstop=4          " how long a tab is represented
set softtabstop=4      " how many spaces to delete when backspacing

augroup language_customizations
    " clear old defs
    autocmd!
    autocmd FileType python setlocal expandtab
    " cindent instead of smartindent to not force hashes going to beginning of line
    " for python comments, that's annoying
    autocmd FileType python setlocal cindent

    " RegExp to find file names + line numbers in quickfix window
    autocmd FileType python setlocal efm=%C\ %.%#,%A\ \ File\ \"%f\"\\,\ line\ %l%.%#,%Z%[%^\ ]%\\@=%m

    autocmd FileType ruby setlocal expandtab
    autocmd FileType ruby setlocal tabstop=2
    autocmd FileType ruby setlocal softtabstop=2
    autocmd FileType ruby setlocal shiftwidth=2
    autocmd BufRead,BufNewFile Podfile set filetype=ruby

    autocmd FileType javascript setlocal noexpandtab
    autocmd FileType yaml setlocal expandtab
    autocmd FileType yaml setlocal tabstop=2
    autocmd FileType yaml setlocal softtabstop=2
    autocmd FileType yaml setlocal shiftwidth=2
    autocmd FileType yaml setlocal expandtab

    autocmd FileType sls setlocal tabstop=4
    autocmd FileType sls setlocal softtabstop=4
    autocmd FileType sls setlocal shiftwidth=4
    autocmd FileType sls setlocal expandtab

    autocmd FileType go setlocal tabstop=4
    autocmd FileType go setlocal softtabstop=4
    autocmd FileType go setlocal shiftwidth=4

    autocmd FileType go setlocal efm=%E%.%#FAIL:\ %m,%C%.%#:[0-9]%#:\ %m,%C%.%#\ %f:%l,%Z%.%#stacktrace:\ %#%f:%l,%-G%.%#,#E%f:%l:\ %m
" --- FAIL: TestCanNegotiateEncryptedSessionConnection (0.06 seconds)
" 	expectations.go:71: expected &protocol.packetBuffer{packets:[]interface {}{}, wUpgrader:(protocol.WriterFactory)(nil), rUpgrader:(protocol.ReaderFactory<...>ol.packetBuffer{packets:[]interface {}{}, wUpgrader:(protocol.WriterFactory)(nil), rUpgrader:(protocol.ReaderFactory)(nil)} (*protocol.packetBuffer) No packets to read!
" 		 stacktrace: /Users/jeff/workspace/mc/src/mc/protocol/connection_test.go:246
" 		                 inside mc/protocol.TestCanNegotiateEncryptedSessionConnection
" 		             /Users/jeff/.gvm/gos/go1.1/src/pkg/testing/testing.go:354
" 		                 inside testing.tRunner
" 		             /Users/jeff/.gvm/gos/go1.1/src/pkg/runtime/proc.c:1223


    autocmd FileType html setlocal tabstop=2
    autocmd FileType html setlocal softtabstop=2
    autocmd FileType html setlocal shiftwidth=2
    autocmd FileType html setlocal noexpandtab

    autocmd FileType css setlocal noexpandtab

    autocmd FileType clojure set lisp

    " make requires tabs
    autocmd FileType make setlocal noexpandtab
    autocmd FileType make setlocal nosmartindent

    autocmd FileType php setlocal noexpandtab
augroup END

"""""""""""""""""""""""""""""""""""""""""""""""
"              PLUGIN CONFIG                  "
"""""""""""""""""""""""""""""""""""""""""""""""

""""""""""
" Python "
""""""""""
let python_highlight_all = 1
let python_slow_sync = 1
let python_highlight_builtin_objs = 1
let python_highlight_builtin_funcs = 1
let g:pymode_lint_write = 0
let g:pymode_rope = 1
let g:pymode_rope_auto_project = 1

"""""""""""""
" Google Go "
"""""""""""""

augroup googlego_customizations
    autocmd!
    autocmd Filetype go autocmd BufWritePre <buffer> Fmt
augroup END

"""""""""""
" Clojure "
"""""""""""
" HighlightBuiltins doesn't work....
augroup clojure_customizations
    autocmd!
    autocmd Filetype clojure highlight link ClojureSpecial Define
augroup END

" Rainbow parens
autocmd VimEnter * RainbowParenthesesToggle
autocmd Syntax * RainbowParenthesesLoadRound
autocmd Syntax * RainbowParenthesesLoadSquare
autocmd Syntax * RainbowParenthesesLoadBraces

"""""""""
" CtrlP "
"""""""""
let g:ctrlp_map = '<leader>f'
let g:ctrlp_cmd = 'CtrlP' " 'CtrlPMixed'
let g:ctrlp_working_path_mode = 'ra'
let g:ctrlp_user_command = 'ag %s -i --nocolor --nogroup --hidden
      \ --ignore .git
      \ --ignore .svn
      \ --ignore .hg
      \ --ignore .DS_Store
      \ --ignore "**/*.pyc"
      \ -g ""'

"""""""""""""""""
" vim-auto-save "
"""""""""""""""""
let g:auto_save = 1 " enable
let g:auto_save_in_insert_mode = 0 " don't autosave in insert mode
set updatetime=1000 " wait 1 second before saving

"""""""""""""""
" neocomplete "
"""""""""""""""
inoremap <expr><C-g> neocomplete#cancel_popup()

let g:neocomplete#enable_at_startup = 1
let g:neocomplete#enable_smart_case = 1
let g:neocomplete#sources#syntax#min_keyword_length = 3
let g:neocomplete#lock_buffer_name_pattern = '\*ku\*'
let g:neocomplete#force_overwrite_completefunc = 1

autocmd FileType css setlocal omnifunc=csscomplete#CompleteCSS
autocmd FileType html,markdown setlocal omnifunc=htmlcomplete#CompleteTags
autocmd FileType javascript setlocal omnifunc=javascriptcomplete#CompleteJS
autocmd FileType python setlocal omnifunc=pythoncomplete#Complete
autocmd FileType xml setlocal omnifunc=xmlcomplete#CompleteTags

if !exists('g:neocomplete#sources#omni#input_patterns')
  let g:neocomplete#sources#omni#input_patterns = {}
endif
let g:neocomplete#sources#omni#input_patterns.php = '[^. \t]->\h\w*\|\h\w*::'
let g:neocomplete#sources#omni#input_patterns.c = '[^.[:digit:] *\t]\%(\.\|->\)'
let g:neocomplete#sources#omni#input_patterns.cpp = '[^.[:digit:] *\t]\%(\.\|->\)\|\h\w*::'

"""""""""""""""
" neosnippet  "
"""""""""""""""
let g:neosnippet#snippets_directory = "$HOME/.vim/snippets/"

""""""""""""""""""
" vim-easy-align "
""""""""""""""""""
vmap <Enter> <Plug>(EasyAlign)
nmap <Leader>a <Plug>(EasyAlign)

""""""""""""""""
" vim-markdown "
""""""""""""""""
let g:vim_markdown_initial_foldlevel=99

""""""""""
" vim-go "
""""""""""
autocmd FileType go nmap <Leader>i <Plug>(go-implements)
autocmd FileType go nmap <Leader>r <Plug>(go-rename)
autocmd FileType go nmap <Leader>c :!gotags -R $GOPATH > $GOPATH/tags<cr>
let g:go_fmt_command = "goimports"
let g:go_fmt_autosave = 1

"""""""""""""""""
" experimental  "
"""""""""""""""""

" For snippet_complete marker.
if has('conceal')
    set conceallevel=2 concealcursor=i
endif


